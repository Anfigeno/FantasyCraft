AccionesBase from "@lib/AccionesBase.civet"
{ GuildMember } from "discord.js"

export default class NuevoUsuario < AccionesBase
  public @ darleLaBienvenida(miembro: GuildMember): Promise<void>
    { mensajesDelSistema, canalesImportantes } := @api
    await mensajesDelSistema.obtener()
    partesBienvenida := mensajesDelSistema.bienvenida
      .split "{usuario.nombre}"
      .join `${miembro.user.username}`
      .split "{usuario}"
      .join `${miembro}`
      .split "\n"
      
    bienvenida := 
      titulo: partesBienvenida[0]
      descripcion: partesBienvenida
        .slice 1
        .join "\n"

    embed := @crearEmbedEstilizado() |> await |>
      .setTitle bienvenida.titulo
      .setDescription bienvenida.descripcion
      .setThumbnail miembro.user.avatarURL()

    await canalesImportantes.obtener()
    canalDeBienvenidas := await @obtenerCanal 
      canalesImportantes.idBienvenidas, miembro.guild

    if !canalDeBienvenidas.isTextBased()
      @log.warn "El canal de bienvenidas no es un canal de texto"
    else 
      canalDeBienvenidas.send
        embeds: [ embed ]